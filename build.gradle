buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
        maven {
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6+'
    }
}

// Gradle default tasks
defaultTasks 'build', 'install'

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'org.spongepowered.mixin'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

archivesBaseName = project.name.toLowerCase()

minecraft {
    version = "1.12.2-14.23.5.2768"
    runDir = "run"
    mappings = 'snapshot_20180808'
    useDepAts = true
    makeObfSourceJar = false
}

mixin {
    add sourceSets.main, "mixins.sgcraft.refmap.json"
}

deobfMcMCP { failOnAtError = false }
deobfMcSRG { failOnAtError = false }

jar {
    manifest.attributes(
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'MixinConfigs': 'mixins.sgcraft.json',
            'FMLCorePluginContainsFMLMod': 'true',
            'ForceLoadAsMod': true
    )
    classifier = 'original'
}

repositories {
    maven { url = "http://files.minecraftforge.net/maven" } // spongeforge
    maven { url = "http://maven.cil.li/" }
    maven {
        name = 'oss-sonatype-public'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven { url = "http://maven.ic2.player.to" }
    maven { url = "http://repo.spongepowered.org/maven" }
    maven { url = "http://cc.crzd.me/maven/" }
}

dependencies {
    compile "org.spongepowered:mixin:0.7.11-SNAPSHOT"
    compile 'org.spongepowered:configurate-yaml:3.6'
    compile 'org.spongepowered:configurate-core:3.6'
    compile "li.cil.oc:OpenComputers:MC1.12.1-1.7.1.52"
    compile "net.industrial-craft:industrialcraft-2:2.8.93-ex112:api"
    deobfCompile "dan200.computercraft:ComputerCraft:1.80pr1-build4"
    deobfCompile ('com.almuradev:malisiscore:1.12.2-6.4.2-SNAPSHOT') {
        transitive = false
    }
}

if (JavaVersion.current().isJava8Compatible() || JavaVersion.current().isJava9Compatible()) {
    tasks.withType(Javadoc) {
        // disable the crazy super-strict doclint tool in Java 8
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

compileJava {
    options.compilerArgs += [
            '-Xlint:-processing',
    ]
    options.deprecation = false
    options.encoding = 'utf8'
}

processResources {

    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.mcversion

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

shadowJar {
    mainSpec.sourcePaths.clear()
    dependsOn reobfJar

    classifier ''

    exclude 'dummyThing'
    relocate 'com.typesafe.config', 'configurate.typesafe.config'
    dependencies {
        include dependency('org.spongepowered:configurate-core')
        include dependency('org.spongepowered:configurate-yaml')
        include dependency('org.spongepowered:mixin')
    }
    afterEvaluate {
        from zipTree(reobfJar.jar)
    }
}

artifacts {
    archives shadowJar
}

sourceSets.main {
	compileClasspath += files("lib")
	java.srcDirs "src/base", "src/mod"
	resources.srcDirs "src/resources"
}


